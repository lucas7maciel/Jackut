# User Story 8 - Criacao de novos relacionamentos - Permita a um usuario cadastrado do Jackut estabelecer outros tipos de relacionamentos dentro da rede, alem de amizade.

zerarSistema

criarUsuario login=jpsauve senha=sauvejp nome="Jacques Sauve"
s1=abrirSessao login=jpsauve senha=sauvejp

##################
# Testes da relacao fa-idolo
# Regras: um usuario pode adicionar outro como idolo e passa a ser chamado seu fa; essa relacao e independente do fato de os usuarios serem amigos;
# a informacao de que usuario e fa de quem e pública
##################

criarUsuario login=fadejacques senha=seuqcajedaf nome="Fa de Jacques"
s2=abrirSessao login=fadejacques senha=seuqcajedaf

expect false ehAmigo login=fadejacques amigo=jpsauve
expect false ehAmigo login=jpsauve amigo=fadejacques
expect false ehFa login=fadejacques idolo=jpsauve
expect false ehFa login=jpsauve idolo=fadejacques

adicionarIdolo id=${s2} idolo=jpsauve

expect false ehAmigo login=fadejacques amigo=jpsauve
expect false ehAmigo login=jpsauve amigo=fadejacques
expect true ehFa login=fadejacques idolo=jpsauve
expect false ehFa login=jpsauve idolo=fadejacques

expect {} getFas login=fadejacques
expect {fadejacques} getFas login=jpsauve

# um usuario pode ter mais de um fa
criarUsuario login=fa2dejacques senha=seuqcajedaf nome="Fa 2 de Jacques"
s3=abrirSessao login=fa2dejacques senha=seuqcajedaf

adicionarIdolo id=${s3} idolo=jpsauve

expect true ehFa login=fa2dejacques idolo=jpsauve
expect false ehFa login=jpsauve idolo=fa2dejacques

expect {fadejacques,fa2dejacques} getFas login=jpsauve

# um usuario pode ter mais de um idolo
adicionarIdolo id=${s3} idolo=fadejacques

expect true ehFa login=fa2dejacques idolo=jpsauve
expect true ehFa login=fa2dejacques idolo=fadejacques

expect {} getFas login=fa2dejacques
expect {fadejacques,fa2dejacques} getFas login=jpsauve
expect {fa2dejacques} getFas login=fadejacques

# tratamento de erros

expectError "Usuario ja esta adicionado como idolo." adicionarIdolo id=${s3} idolo=jpsauve
expectError "Usuario nao cadastrado." adicionarIdolo id=${s3} idolo=qwerty
expectError "Usuario nao pode ser fa de si mesmo." adicionarIdolo id=${s3} idolo=fa2dejacques

##################
# Testes da relacao de paquera
# Regras: um usuario pode adicionar outro como paquera, e esta informacao e privada para quem adicionou; nao precisam ser amigos; entretanto, se ambos se adicionarem como paquera mutuamente, um recado automatico do
# sistema e enviado para ambos
##################

criarUsuario login=paqueradejacques senha=abcdef nome="Paquera de Jacques"
s2=abrirSessao login=paqueradejacques senha=abcdef

expect false ehAmigo login=paqueradejacques amigo=jpsauve
expect false ehAmigo login=jpsauve amigo=paqueradejacques
expect false ehPaquera id=${s2} paquera=jpsauve
expect false ehPaquera id=${s1} paquera=paqueradejacques

adicionarPaquera id=${s2} paquera=jpsauve

expect false ehAmigo login=paqueradejacques amigo=jpsauve
expect false ehAmigo login=jpsauve amigo=paqueradejacques
expect true ehPaquera id=${s2} paquera=jpsauve
expect false ehPaquera id=${s1} paquera=paqueradejacques
expect {jpsauve} getPaqueras id=${s2}
expect {} getPaqueras id=${s1}

# agora jpsauve adiciona paqueradejacques como paquera

expectError "Nao ha recados." lerRecado id=${s1}
expectError "Nao ha recados." lerRecado id=${s2}

adicionarPaquera id=${s1} paquera=paqueradejacques

expect false ehAmigo login=paqueradejacques amigo=jpsauve
expect false ehAmigo login=jpsauve amigo=paqueradejacques
expect true ehPaquera id=${s2} paquera=jpsauve
expect true ehPaquera id=${s1} paquera=paqueradejacques
expect {jpsauve} getPaqueras id=${s2}
expect {paqueradejacques} getPaqueras id=${s1}

# um recado e enviado a ambos pelo sistema

expect "Paquera de Jacques e seu paquera - Recado do Jackut." lerRecado id=${s1}
expect "Jacques Sauve e seu paquera - Recado do Jackut." lerRecado id=${s2}

# um usuario pode ter mais de um paquera

criarUsuario login=paquera2dejacques senha=abcdef nome="Paquera 2 de Jacques"
s3=abrirSessao login=paquera2dejacques senha=abcdef

adicionarPaquera id=${s3} paquera=jpsauve
adicionarPaquera id=${s3} paquera=paqueradejacques

expect {jpsauve,paqueradejacques} getPaqueras id=${s3}
expect {jpsauve} getPaqueras id=${s2}
expect {paqueradejacques} getPaqueras id=${s1}

# tratamento de erros

expectError "Usuario ja esta adicionado como paquera." adicionarPaquera id=${s3} paquera=jpsauve
expectError "Usuario nao cadastrado." adicionarPaquera id=${s3} paquera=qwerty
expectError "Usuario nao pode ser paquera de si mesmo." adicionarPaquera id=${s3} paquera=paquera2dejacques

################
# Testes da relacao de inimizade
# Regras: um usuario pode informar ao Jackut que outro usuario e seu inimigo; um inimigo nao pode lhe adicionar como amigo, paquera ou fa, e todas as mensagens que chegam dele sao automaticamente descartadas
################

criarUsuario login=inimigodejacques senha=abcdef nome="Inimigo de Jacques"
s2=abrirSessao login=inimigodejacques senha=abcdef

adicionarInimigo id=${s2} inimigo=jpsauve

expect false ehAmigo login=jpsauve amigo=inimigodejacques
expect false ehFa login=jpsauve idolo=inimigodejacques
expect false ehPaquera id=${s1} paquera=inimigodejacques

expectError "Funcao invalida: Inimigo de Jacques e seu inimigo." adicionarAmigo id=${s1} amigo=inimigodejacques
expectError "Funcao invalida: Inimigo de Jacques e seu inimigo." adicionarIdolo id=${s1} idolo=inimigodejacques
expectError "Funcao invalida: Inimigo de Jacques e seu inimigo." adicionarPaquera id=${s1} paquera=inimigodejacques
expectError "Funcao invalida: Inimigo de Jacques e seu inimigo." enviarRecado id=${s1} destinatario=inimigodejacques recado="fhdjfsd"

# usuario pode ter mais de um inimigo

criarUsuario login=inimigo2dejacques senha=abcdef nome="Inimigo 2 de Jacques"
s3=abrirSessao login=inimigo2dejacques senha=abcdef

adicionarInimigo id=${s3} inimigo=jpsauve
adicionarInimigo id=${s3} inimigo=inimigodejacques

expectError "Funcao invalida: Inimigo 2 de Jacques e seu inimigo." adicionarAmigo id=${s1} amigo=inimigo2dejacques
expectError "Funcao invalida: Inimigo 2 de Jacques e seu inimigo." adicionarAmigo id=${s2} amigo=inimigo2dejacques

# tratamento de erros

expectError "Usuario ja esta adicionado como inimigo." adicionarInimigo id=${s2} inimigo=jpsauve
expectError "Usuario nao cadastrado." adicionarInimigo id=${s2} inimigo=qwerty
expectError "Usuario nao pode ser inimigo de si mesmo." adicionarInimigo id=${s2} inimigo=inimigodejacques

# teste de persistência

criarUsuario login=jacquespersistente senha=abcdef nome="Jacques Persistente"

criarUsuario login=fapersistentedejacques senha=seuqcajedaf nome="Fa Persistente de Jacques"
s2=abrirSessao login=fapersistentedejacques senha=seuqcajedaf
adicionarIdolo id=${s2} idolo=jacquespersistente

criarUsuario login=paquerapersistentedejacques senha=abcdef nome="Paquera Persistente de Jacques"
s3=abrirSessao login=paquerapersistentedejacques senha=abcdef
adicionarPaquera id=${s3} paquera=jacquespersistente

criarUsuario login=inimigopersistentedejacques senha=abcdef nome="Inimigo Persistente de Jacques"
s4=abrirSessao login=inimigopersistentedejacques senha=abcdef
adicionarInimigo id=${s4} inimigo=jacquespersistente

encerrarSistema
# quit